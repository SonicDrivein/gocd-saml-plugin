buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath "org.owasp:dependency-check-gradle:1.4.5"
  }
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: "org.owasp.dependencycheck"
apply plugin: 'maven-publish'
apply plugin: "jacoco"

group = 'org.sonicdrivein'

version = '0.0.1'

project.ext.pluginDesc = [
  id         : 'cd.go.auth.saml',
  version    : project.version,
  goCdVersion: '17.3.0',
  name       : 'Saml plugin for gocd',
  description: 'Saml plugion for gocd',
  vendorName : "Sonic America's drive in",
  vendorUrl  : 'https://github.com/sonicdrivein/gocd-saml-plugin'
]

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
  mavenLocal()
}

dependencyCheck {
    failBuildOnCVSS = 7
    suppressionFile = './suppression.xml'
    skipConfigurations = ['checkstyle']
    format = 'ALL'
}

dependencies {
  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '17.2.0'
  compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
  compile group: 'commons-io', name: 'commons-io', version: '2.5'
  compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
  compile group: 'org.opensaml', name: 'opensaml', version: '2.5.1-1'
  compile group: 'org.opensaml', name: 'xmltooling', version: '1.3.2-1'
  compile group: 'org.opensaml', name: 'openws', version: '1.4.2-1'
  compile "xalan:xalan:2.7.2" // solves CVE-2014-0107
  compile "commons-collections:commons-collections:3.2.2" // solves CVE-2015-6420


  testCompile group: 'cd.go.plugin', name: 'go-plugin-api', version: '16.11.0'
  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.mockito', name: 'mockito-core', version: '2.2.28'
}

processResources {
  from("src/main/resource-templates") {
    filesMatching('plugin.xml') {
      expand project.pluginDesc
    }

    filesMatching('plugin.properties') {
      expand project.pluginDesc
    }
  }
}

jar {
  from(configurations.compile) {
    into "lib/"
  }

//    This is useful for debugging
  from(sourceSets.main.java) {
    into "/"
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}

install.finalizedBy assemble
